plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdk buildConfig.compileSdkVersion

    defaultConfig {
        applicationId applicationAppId

        minSdk buildConfig.minSdkVersion
        targetSdk buildConfig.targetSdkVersion

        versionCode buildConfig.version.code
        versionName buildConfig.version.name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resConfigs 'en'

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
    }

    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('../keystore/debug.keystore')
            storePassword 'android'
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
            manifestPlaceholders = [
                    crashlyticsEnabled: false,
                    performanceEnabled: false,
            ]
            signingConfig signingConfigs.debug
        }
        release {
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
            manifestPlaceholders = [
                    crashlyticsEnabled: true,
                    performanceEnabled: true,
            ]
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    sourceSets {
        development {
            java {
                srcDirs = ['src/main/java']
            }
        }
        production {
            java {
                srcDirs = ['src/main/java']
            }
        }
        main {
            res.srcDirs = ['src/main/res']
        }
    }

    productFlavors {
        development {
            dimension "default"
            applicationIdSuffix ".development"
            resValue "string", "app_name", "$applicationName - Development"
        }
        production {
            dimension "default"
            resValue "string", "app_name", applicationName
        }
    }

    buildFeatures.dataBinding = true

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // Custom Libraries
    implementation project(':core')
}

apply from: '../dependencies.gradle'